/*
 * Copyright 2018 Idealnaya rabota LLC
 * Licensed under Multy.io license.
 * See LICENSE for details
 */

apply plugin: 'com.android.application'
apply plugin: 'realm-android'
apply from: 'constants.gradle'
apply from: 'scripts.gradle'

android {
    compileSdkVersion 27
    defaultConfig {
        vectorDrawables.useSupportLibrary = true
        applicationId "io.multy"
        minSdkVersion 21
        targetSdkVersion 27
        versionCode 17
        versionName "1.1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        
//        ndk {
//            abiFilters 'x86', 'armeabi-v7a', 'arm64-v8a'
//        }
        externalNativeBuild {
            cmake {
                cppFlags "-std=c++11"
                arguments DMULTY_ANDROID_PATH_TO_JNI_WRAPPER
                arguments "-DANDROID_STL=c++_shared"
                arguments "-DMULTY_WITH_TESTS=ON"
            }
        }
        setProperty("archivesBaseName", "${applicationId}_${versionName}_${releaseTime()}")
        buildConfigField "String", "BUILD_COMMIT_HASH", "\"${getCommitHash()}\""
        buildConfigField "String", "BUILD_COMMIT_BRANCH", "\"${getCommitBranch()}\""
        buildConfigField "String", "BUILD_COMMIT_DESCRIPTION", "\"${getCommitDescription()}\""
    }
    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            manifestPlaceholders = [crashlyticsEnabled: true, analyticsDeactivated: false]
            resValue "string", "account_name", "Multy.io"
            resValue "string", "account_type", "io.multy"
        }
        externalNativeBuild {
            cmake {
                path CMAKE_PATH
            }
        }
        debug {
            applicationIdSuffix ".debug"
            resValue "string", "account_name", "Multy.io.debug"
            resValue "string", "account_type", "io.multy.debug"
        }
    }
    flavorDimensions "default"
    productFlavors {
        prod {
            resValue "string", "base_url", "https://api.multy.io/"
        }
        stage {
            resValue "string", "base_url", "https://stage.multy.io/"
        }
        multyTest {
            resValue "string", "base_url", "http://test.multy.io/"
        }
    }
    variantFilter { variant ->
        if (variant.buildType.name.contains("externalNativeBuild")) {
            variant.setIgnore(true)
        }
    }
    compileOptions {
        targetCompatibility JavaVersion.VERSION_1_8
        sourceCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.android.support:appcompat-v7:27.1.1'
    implementation 'com.android.support.constraint:constraint-layout:1.1.1'
    implementation 'com.android.support:recyclerview-v7:27.1.1'
    implementation 'com.android.support:cardview-v7:27.1.1'
    implementation 'com.android.support:design:27.1.1'
    implementation 'com.squareup.retrofit2:retrofit:2.3.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.3.0'
    implementation 'com.squareup.retrofit2:adapter-java8:2.3.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.8.1'
    implementation 'io.reactivex.rxjava2:rxjava:2.1.9'
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.2'
    implementation 'com.jakewharton.rxbinding2:rxbinding:2.1.1'
    implementation 'com.jakewharton.retrofit:retrofit2-rxjava2-adapter:1.0.0'
    implementation 'com.jakewharton.rxbinding2:rxbinding:2.1.1'
    implementation 'com.google.code.gson:gson:2.8.2'
    implementation 'com.jakewharton:butterknife:8.8.1'
    implementation 'com.jakewharton.timber:timber:4.6.0'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.1'
    implementation 'com.google.android.gms:play-services-gcm:15.0.1'
    implementation 'com.google.firebase:firebase-core:16.0.0'
    implementation('com.crashlytics.sdk.android:crashlytics:2.7.1@aar') {
        transitive = true
    }
    implementation 'com.google.firebase:firebase-core:16.0.0'
    implementation 'com.google.firebase:firebase-messaging:17.0.0'
    implementation 'android.arch.lifecycle:extensions:1.1.1'
    testImplementation 'android.arch.core:core-testing:1.1.1'
    implementation 'android.arch.lifecycle:reactivestreams:1.1.1'
    implementation 'com.samwolfand.oneprefs:one-prefs:1.0'
    implementation 'com.google.zxing:core:3.3.2'
    implementation 'com.google.zxing:javase:3.3.2'
    implementation 'me.dm7.barcodescanner:zbar:1.9.8'
    implementation 'io.branch.sdk.android:library:2.17.1'
    implementation 'com.scottyab:rootbeer-lib:0.0.6'
    implementation('io.socket:socket.io-client:0.9.+') {
        exclude group: 'org.json', module: 'json'
    }
    implementation 'com.github.tozny:java-aes-crypto:1.1.0'
    implementation 'com.github.khirr:Android-Foreground-Detection:1.0.1'
    implementation 'org.greenrobot:eventbus:3.1.1'
    implementation 'com.airbnb.android:lottie:2.5.4'
    implementation 'net.cachapa.expandablelayout:expandablelayout:2.9.2'
    implementation 'com.github.guilhe:circular-progress-view:1.3.1'
    implementation 'cn.aigestudio.wheelpicker:WheelPicker:1.1.2'
}

apply plugin: 'com.google.gms.google-services'
apply plugin: 'io.fabric'